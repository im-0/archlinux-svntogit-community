# Maintainer: Robin Broda <coderobe @ archlinux.org>
pkgname=nebula
pkgver=1.6.1
pkgrel=1
pkgdesc='A scalable overlay networking tool with a focus on performance, simplicity and security'
arch=('x86_64')
url='https://github.com/slackhq/nebula'
license=('MIT')
depends=()
makedepends=('go')
options=('!lto')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        '0001-Remove-net-netip-dependency-to-build-with-golang-1.1.patch')
sha256sums=('9c343d998d2eab9473c3bf73d434b8a382d90b1f73095dd1114ecaf2e1c0970f'
            '1dd97468b03c2c3c0e4fe760537b18be455071a3cf52dda8e6491cfeb49aa68f')

prepare() {
  patch -d "${pkgbase}-${pkgver}" -p1 <0001-Remove-net-netip-dependency-to-build-with-golang-1.1.patch
}

build() {
  cd "${pkgname}-${pkgver}"

  export CGO_ENABLED="0"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  for bin in nebula{,-cert,-service}
  do go build \
    -ldflags "-X main.Build=${pkgver}" \
    -o "${bin}" "./cmd/${bin}"
  done
}

check() {
  cd "${pkgname}-${pkgver}"

  go test -v ./...
}

package() {
  cd "${pkgname}-${pkgver}"

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 dist/arch/nebula.service "${pkgdir}/usr/lib/systemd/system/nebula.service"

  for bin in nebula{,-cert,-service}
  do install -Dm755 "${bin}" "${pkgdir}/usr/bin/${bin}"
  done
}
